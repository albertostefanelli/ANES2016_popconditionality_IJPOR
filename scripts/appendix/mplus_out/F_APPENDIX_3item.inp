TITLE:
F_APPENDIX_3item
DATA:
FILE = "F_APPENDIX_3item.dat";
 
VARIABLE:
NAMES = spending_extr defence_extr medical_extr garant_inc_extr blacks_extr
     envir_extr affirm_extr wagner_aff V161513 V161514 V161515 V161516 V162259
     V162260 V162264 V161270 V161361x V161267 V161241 FEM BLACK ASIAN HISP OTHERR
     bernie perc_pol id_iden7 pid_3 WEAK STRONG V162256 V162257 V162215 V162216
     V162217 V162218 V160102 V160201; 
 MISSING=.;
 USEVARIABLES = spending_extr
defence_extr medical_extr
garant_inc_extr blacks_extr
envir_extr affirm_extr
wagner_aff V161513 V161514
V161515 V161516 V162259
V162260 V162264 V161270
V161361x V161267 V161241 FEM
BLACK ASIAN HISP OTHERR
bernie perc_pol id_iden7
pid_3 WEAK STRONG V162256
V162257 V162215 V162216
V162217 V162218 V160102
V160201 ;
CATEGORICAL = V161513 V161514 V161515 V161516;
CLUSTER = V160201;
class = pid (2);
KNOWNCLASS = pid (pid_3=1 pid_3=2);
WEIGHT=V160102;
ANALYSIS:
TYPE = MIXTURE COMPLEX;
             ESTIMATOR IS MLR;
             !MODEL = CONFIGURAL METRIC SCALAR;
             ALGORITHM=INTEGRATION;
             !ITER=300;
             PROCESSORS = 2;
       
MODEL:

  %overall%
  KN BY V161513;
  KN BY V161514;
  KN BY V161515;
  KN BY V161516;

  pop BY V162259;
  pop BY V162260;
  pop BY V162264;

  pol_int BY V162256;
  pol_int BY V162257;

  EFFE BY V162215;
  EFFE BY V162216;

  EFFI BY V162217;
  EFFI BY V162218;

  V162217 WITH V162216;

  EXT BY spending_extr;
  EXT BY defence_extr;
  EXT BY medical_extr;
  EXT BY garant_inc_extr;
  EXT BY blacks_extr;
  EXT BY envir_extr;
  EXT BY affirm_extr;

  wagner_aff WITH EXT;

   EXT ON POP
  WEAK
  STRONG
  EFFI      ! internal efficacy
  EFFE      ! external efficacy
  V161241   ! religiosity importance yes no
  pol_int   ! pol interest
  KN        ! knowledge
  perc_pol  ! percieved polarization
  id_iden7  ! ideological identity strength
  bernie    ! voted for bernie
  V161270   ! education
  V161361x  ! income
  V161267   ! age
  FEM       ! gender
  !!!! RACE !!!!
  BLACK
  ASIAN
  HISP
  OTHERR;


   wagner_aff ON POP
  WEAK
  STRONG
  EFFI      ! internal efficacy
  EFFE      ! external efficacy
  V161241   ! religiosity importance yes no
  pol_int   ! pol interest
  KN        ! knowledge
  perc_pol  ! percieved polarization
  id_iden7  ! ideological identity strength
  bernie    ! voted for bernie
  V161270   ! education
  V161361x  ! income
  V161267   ! age
  FEM       ! gender
  !!!! RACE !!!!
  BLACK
  ASIAN
  HISP
  OTHERR;

  !!!!!!!!!!!!!!!!!!!!!!!!
  !!!!!!! DEMOCRATS !!!!!!
  !!!!!!!!!!!!!!!!!!!!!!!!

  %pid#1%

  !! EXTREMITY !!

  [EXT] (extb0g1);         ! free intercept
  EXT (vareg1);            ! free residual

  !!! freely estimated slopes !!!
  EXT ON POP (extb1g1);
  EXT ON bernie (extb2g1); !fixed

  !! AFFECTIVE !!

  [wagner_aff] (affb0g1);  ! free intercept
  wagner_aff (varag1);     ! free residual

  !!! freely estimated slopes !!!
  wagner_aff ON POP (affb1g1);
  wagner_aff ON bernie (affb2g1);

  wagner_aff WITH EXT (covg1);


  EXT ON EFFI (exts01g1) ! internal efficacy
  EFFE        (exts02g1) ! external efficacy
  V161241     (exts03g1) ! religiosity importance yes no
  pol_int     (exts04g1) ! pol interest
  KN          (exts05g1) ! knowledge
  perc_pol    (exts06g1) ! percieved polarization
  id_iden7    (exts07g1) ! ideological identity strength
  V161270     (exts11g1) ! education
  V161361x    (exts12g1) ! income
  V161267     (exts13g1) ! age
  FEM         (exts14g1) ! gender
  BLACK       (exts15g1)
  ASIAN       (exts16g1)
  HISP        (exts17g1)
  OTHERR      (exts18g1);

  wagner_aff ON EFFI (affs01g1) ! internal efficacy
  EFFE        (affs02g1)        ! external efficacy
  V161241     (affs03g1)        ! religiosity importance yes no
  pol_int     (affs04g1)        ! pol interest
  KN          (affs05g1)        ! knowledge
  perc_pol    (affs06g1)        ! percieved polarization
  id_iden7     (affs07g1)        ! ideological identity strength
  V161270     (affs11g1)        ! education
  V161361x    (affs12g1)        ! income
  V161267     (affs13g1)        ! age
  FEM         (affs14g1)        ! gender
  BLACK       (affs15g1)
  ASIAN       (affs16g1)
  HISP        (affs17g1)
  OTHERR      (affs18g1);


  !!!!!!!!!!!!!!!!!!!!!!!!!!!
  !!!!!!! REPUBLICANS  !!!!!!
  !!!!!!!!!!!!!!!!!!!!!!!!!!!

  %pid#2%

  !! EXTREMITY !!

  [EXT] (extb0g3);        ! free intercept
  [spending_extr@0];      ! need to fix observed 1 mean manifest to zero to estimte intercept
  EXT (vareg3);           ! free residual

  !!! freely estimated slopes !!!
  EXT ON POP (extb1g3);
  EXT ON bernie (extb2g1);! fixed

  !! AFFECTIVE !!

  [wagner_aff] (affb0g3); ! free intercept
  wagner_aff (varag3);    ! free residual

  !!! freely estimated slopes !!!
  wagner_aff ON POP (affb1g3);
  wagner_aff ON bernie (affb2g1);

  wagner_aff WITH EXT (covg3);

  EXT ON EFFI (exts01g1) ! internal efficacy
  EFFE        (exts02g1) ! external efficacy
  V161241     (exts03g1) ! religiosity importance yes no
  pol_int     (exts04g1) ! pol interest
  KN          (exts05g1) ! knowledge
  perc_pol    (exts06g1) ! percieved polarization
  id_iden7    (exts07g1) ! ideological identity strength
  V161270     (exts11g1) ! education
  V161361x    (exts12g1) ! income
  V161267     (exts13g1) ! age
  FEM         (exts14g1) ! gender
  BLACK       (exts15g1)
  ASIAN       (exts16g1)
  HISP        (exts17g1)
  OTHERR      (exts18g1);

  wagner_aff ON EFFI (affs01g1) ! internal efficacy
  EFFE        (affs02g1)        ! external efficacy
  V161241     (affs03g1)        ! religiosity importance yes no
  pol_int     (affs04g1)        ! pol interest
  KN          (affs05g1)        ! knowledge
  perc_pol    (affs06g1)        ! percieved polarization
  id_iden7    (affs07g1)        ! ideological identity strength
  V161270     (affs11g1)        ! education
  V161361x    (affs12g1)        ! income
  V161267     (affs13g1)        ! age
  FEM         (affs14g1)        ! gender
  BLACK       (affs15g1)
  ASIAN       (affs16g1)
  HISP        (affs17g1)
  OTHERR      (affs18g1);


MODEL CONSTRAINT:

  NEW(extdemrep affdemrep sdl sdh extdeml extdemh extrepl extreph extdiffdem extdiffrep
  affdeml affdemh affrepl affreph affdiffdem affdiffrep);
  !! Difference in the Effect of populism on DVs (slope)
  extdemrep = extb1g1 - extb1g3;
  affdemrep = affb1g1 - affb1g3;
  sdl = -1;
  sdh = 1;
  ! predicted values of ext for -1 +1 sd
  extdeml = extb0g1 + extb1g1*sdl;
  extdemh = extb0g1 + extb1g1*sdh;
  extrepl = extb0g3 + extb1g3*sdl;
  extreph = extb0g3 + extb1g3*sdh;
  ! diff rep dem between low and high predicted v
  extdiffdem = extdeml - extdemh;
  extdiffrep = extrepl - extreph;
  ! predicted values of ext for -1 +1 sd
  affdeml = affb0g1 + affb1g1*sdl;
  affdemh = affb0g1 + affb1g1*sdh;
  affrepl = affb0g3 + affb1g3*sdl;
  affreph = affb0g3 + affb1g3*sdh;
  ! diff rep dem between low and high predicted v
  affdiffdem = affdeml - affdemh;
  affdiffrep = affrepl - affreph;

  
OUTPUT:
STANDARDIZED; CINTERVAL;
SAVEDATA:

  !COVARIANCE = cov.dat; TECH3 =tech3.dat;
  !FILE IS testid_fscores.dat;
  !SAVE IS fscores;
  !FORMAT IS free;
